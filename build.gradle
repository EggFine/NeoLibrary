plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    // 1. 添加 Shadow 插件，用于打包和重定位依赖
    id 'com.gradleup.shadow' version '8.3.6'
}

group = 'com.blbilink'

def baseVersion = "1.21.1"
// 从环境变量读取 CI 提供的构建号
def buildNumber = System.getenv("GITHUB_RUN_NUMBER") ?: "SNAPSHOT"
version = "${baseVersion}.${buildNumber}"


repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "sonatype-oss-snapshots1"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:1.21.1-R0.1-SNAPSHOT")
    compileOnly "dev.folia:folia-api:1.21.1-R0.1-SNAPSHOT"
    compileOnly 'com.google.errorprone:error_prone_annotations:2.28.0'

    implementation 'com.zaxxer:HikariCP:5.1.0'
}

tasks {
    runServer {

    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

// 3. 配置 ShadowJar 任务
shadowJar {
    relocate 'com.zaxxer.hikari', 'com.blbilink.neoLibrary.lib.hikaricp'
    archiveClassifier.set('')
}


tasks.build {
    dependsOn(shadowJar)
}
